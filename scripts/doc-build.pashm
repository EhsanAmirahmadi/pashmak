#!/usr/bin/env pashmak
# Mixes the `doc/` folder parts into `README.md` file

import @file

func build_once ($path)
    $doc_parts = os.listdir($path)
    mem $doc_parts->remove('README.md')
    mem $doc_parts->sort()
    mem $doc_parts->insert(0, 'README.md')

    $total_content = ''

    $i = 0
    section build_once_loop1
        $f = %{ file.open $path + '/' + $doc_parts[$i], 'r' }%
        $content = %{ file.read $f }%
        file.close $f
        $total_content = $total_content + $content + '\n\n\n'
        $i = $i + 1
    mem $i < len($doc_parts); gotoif build_once_loop1

    $note_message = '\n\#\#\#\#\# NOTE: this file is auto generated from `doc` folder. do not change it directly. If you want to edit documentation, edit `doc/` folder files. your changes will built from that folder into this file.\n'

    $total_content = $note_message + '\n' + $total_content
    $total_content = $total_content + $note_message

    $readme_path = $__dir__ + '/../README.md'
    $lang = $path->strip('/')->split('/')[-1]
    if $lang != 'en'
        $readme_path = $__dir__ + '/../README-' + $lang + '.md'
    endif
    $readme_f = %{ file.open $readme_path, 'w' }%
    file.write $readme_f, $total_content
    file.close $readme_f
endfunc

$langs = os.listdir($__dir__ + '/../doc/')
$i = 0
section loop1
    build_once $__dir__ + '/../doc/' + $langs[$i]
    $i = $i + 1
mem $i < len($langs); gotoif loop1

println '\033[32mDocumentation built successfully and README.md generated.\033[0m'
