#!/usr/bin/env pashmak
# Updates files copyright headers

$header_text = '\#\n\# The Pashmak Project\n\# Copyright 2020 parsa shahmaleki <parsampsh@gmail.com>\n\#\n\# This file is part of Pashmak.\n\#\n\# Pashmak is free software: you can redistribute it and/or modify\n\# it under the terms of the GNU General Public License as published by\n\# the Free Software Foundation, either version 3 of the License, or\n\# (at your option) any later version.\n\#\n\# Pashmak is distributed in the hope that it will be useful,\n\# but WITHOUT ANY WARRANTY\; without even the implied warranty of\n\# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n\# GNU General Public License for more details.\n\#\n\# You should have received a copy of the GNU General Public License\n\# along with Pashmak.  If not, see <https://www.gnu.org/licenses/>.\n'

func set_once_header($path)
    $spliter = ('\#' * 73) + '\n\n'
    
    # open file and add header
    $f = open($path, 'r')
    $content = $f->read()
    mem $f->close()

    $new_content = ''

    $parts = $content->split($spliter)
    if len($parts) == 1
        $new_content = $header_text + $spliter + $parts[0]
    else
	# already has the header
        return
    endif

    # add current filename to header
    $only_file_name = $path->replace('\\', '/')->split('/')[-1]
    $new_content = '\#\n\# ' + $only_file_name + '\n' + $new_content

    if $path->replace('\\', '/')->endswith('src' + '/' + 'pashmak.py') or $path->replace('\\', '/')->endswith('tests' + '/' + 'run.py')
        $new_content = '\#!/usr/bin/env python3\n' + $new_content
    endif

    $f = open($path, 'w')
    mem $f->write($new_content)
    mem $f->close()
endfunc

### This class crawls in a path and finds files with specify extension ###
class Crawler
    func __init__($args)
        $this->files_list = []
        $this@get $args[0], $args[1]
    endfunc

    func add_once($args)
        $path = $args[0]
        $extension = $args[1]

        if $path->endswith($extension)
            mem $this->files_list->append(os.path.abspath($path))
        endif
    endfunc

    func get($args)
        $path = $args[0]
        $extension = $args[1]

        $files = os.listdir($path)
        $i = 0
        section loop1
            if os.path.isdir($path + '/' + $files[$i])
                $this@get $path + '/' + $files[$i], $extension
            else
                $this@add_once $path + '/' + $files[$i], $extension
            endif
            $i = $i + 1
        mem $i < len($files); gotoif loop1
    endfunc
endclass

$total_paths = []

$c = %{ new Crawler $__dir__ + '/../src', '.py' }%
$total_paths = [*$total_paths, *$c->files_list]

$c = %{ new Crawler $__dir__ + '/../tests', '.pashmt' }%
$total_paths = [*$total_paths, *$c->files_list]

$c = %{ new Crawler $__dir__ + '/../src/modules', '.pashm' }%
$total_paths = [*$total_paths, *$c->files_list]

$c = %{ new Crawler $__dir__ + '/../examples', '.pashm' }%
$total_paths = [*$total_paths, *$c->files_list]

mem $total_paths->sort()

$i = 0
section loop2
    set_once_header($total_paths[$i])
    $i = $i + 1
mem $i < len($total_paths); gotoif loop2

println '\033[32mHeaders updated successfully\033[0m'
