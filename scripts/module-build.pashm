#!/usr/bin/env pashmak
# Mixes static pashmak internal modules from `src/` directory to `src/core/modules.py` file

import $__dir__ + '/crawler-lib.pashm'

$base_modules_dir = $__dir__ + '/../src'
$modules = {}
$module_files = Crawler($base_modules_dir, '.pashm')->files_list
$module_files->sort()

$i = 0
while $i < len($module_files)
    $module_files[$i] = $module_files[$i][len(os.path.abspath($base_modules_dir)):]
    $i = $i + 1
endwhile

$i = 0
while $i < len($module_files)
    $real_path = $base_modules_dir + $module_files[$i]
    $f = open($real_path, 'r')
    $content = $f->read()
    $f->close()
    $module_name = $module_files[$i]->split('.')[0].strip('/')->replace('/', '.')
    if $module_name->endswith('.__init__')
        $module_name = $module_name[:len($module_name)-9]
    endif
    $modules[$module_name] = $content
    $i = $i + 1
endwhile

$pycode = '""" Internal modules """\n\nmodules = {}\n'
$i = 0
$keys = list($modules->keys())
while $i < len($keys)
    $k = $keys[$i]
    $tmp_module_content = $modules[$k]->replace('\n\n', '\n')
    $tmp_module_content = $tmp_module_content->replace('\n\n', '\n')
    $tmp_module_content = $tmp_module_content->replace('\\n\\', '\\\\n\\\\')
    $new_module_content = ''

    $j = 0
    $lines = $tmp_module_content->strip()->split('\n')
    while $j < len($lines)
        $line = $lines[$j]
        if $line != ''
            $new_module_content = $new_module_content + $line + '\n'
        endif
        $j = $j + 1
    endwhile

    $new_module_content = $new_module_content->strip()
    $pycode = $pycode + '\nmodules["' + $k + '"] = """' + $new_module_content + '"""'

    $i = $i + 1
endwhile

$pycode = $pycode + '\n'

$f = open($__dir__ + '/../src/core/modules.py', 'w')
$f->write($pycode)
$f->close()

println("\033[32mall of modules mixed in 'src/core/modules.py' successfuly\033[0m")
