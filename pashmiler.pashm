#!/usr/bin/env pashmak

if len($argv) <= 1
    println('ERROR: script filename is required')
    exit(1)
endif

class Mixer
    func __init__($args)
        $args = format_args($args)

        $this->total_content = ''
        $this->imported_files = []

        $this->crawl($args[0])
    endfunc

    func crawl($args)
        $path = format_args($args)[0]

        $path = os.path.abspath($path)

        $f = open($path, 'r')
        $content = $f->read()
        $f->close()

        $parsed_code = parser.parse($content, filepath=$path, only_parse=True)

        # find the import commands
        $i = 0
        while $i < len($parsed_code)
            if $parsed_code[$i]['command'] == 'import'
                $import_cmd_arg = False
                try error
                    $__old_dir__ = $__dir__
                    $__old_file__ = $__file__
                    $__dir__ = os.path.dirname($path)
                    $__file__ = $path
                    $import_cmd_arg = python('self.mem = self.eval(' + repr($parsed_code[$i]['args_str']) + ')')
                    $__dir__ = $__old_dir__
                    $__file__ = $__old_file__
                endtry; goto after_error; section error
                section after_error
                if $import_cmd_arg != False
                    if typeof($import_cmd_arg) != tuple
                        $import_cmd_arg = $import_cmd_arg,
                    endif
                    $import_cmd_arg = list($import_cmd_arg)
                    $x = 0
                    while $x < len($import_cmd_arg)
                        if $import_cmd_arg[$x]
                            if $import_cmd_arg[$x][0] == '@'
                                $this->total_content = $this->total_content + $parsed_code[$i]['str'] + '\n'
                                $x = $x + 1
                                continue
                            endif
                        endif
                        $import_cmd_arg[$x] = os.path.abspath($import_cmd_arg[$x])
                        if $import_cmd_arg[$x] not in $this->imported_files
                            $this->crawl($import_cmd_arg[$x])
                            $this->imported_files->append($import_cmd_arg[$x])
                        endif
                        $x = $x + 1
                    endwhile
                endif
            else
                $this->total_content = $this->total_content + $parsed_code[$i]['str'] + '\n'
            endif
            $i = $i + 1
        endwhile
    endfunc
endclass

$mix = Mixer($argv[1])
println($mix->total_content)
